#
# 30dhcp
#

# Enable the DHCP server. Addresses will be given out from the range
# <start-addr> to <end-addr> and from statically defined addresses
# given in dhcp-host options. 
# See db configuration getprop dnsmasq DhcpStatus

{
    use NetAddr::IP;
    my $db = esmith::ConfigDB->open_ro("dhcp") || die "Can't open dhcp database: $!\n";
    my $ndb = esmith::NetworksDB->open_ro() || die "Can't open networks database: $!\n";
    my $tot_range_length = 0;
    my $tftpstatus = $dnsmasq{'tftp-status'} || 'disabled';
    foreach ($db->get_all_by_prop("type" => "range")) {
        my $status = $_->prop('status') || 'disabled';
        if ( $status eq 'enabled') {
            my $interface = $_->key;
            my $netmask = $ndb->get($interface)->prop('netmask') || next;
            my $ipaddr = $ndb->get($interface)->prop('ipaddr') || next;
            my $role = $ndb->get($interface)->prop('role') || '';
            my $start = $_->prop('DhcpRangeStart') || next;
            my $end = $_->prop('DhcpRangeEnd') || next;
            my $start_ip = new NetAddr::IP($start);
            my $end_ip = new NetAddr::IP($end);
            my $range_length = $end_ip->numeric - $start_ip->numeric;
            $tot_range_length += $range_length;
            my $lease = $_->prop('DhcpLeaseTime') || '86400';
            $OUT .= 'dhcp-range=set:' . $_->key . ',' . join(',',$start,$end,$netmask,$lease) . "\n";

            my $domain = $_->prop('DhcpDomain') || '';
            if ($domain ne '') {
                $OUT .= 'domain=' . join(',',$domain,$start,$end) . "\n";
            }

            # Send router configuration to clients
            my $router = $_->prop('DhcpGatewayIP') || '';
            if ($router eq '') {
                my $i = $ndb->get($_->key);
                if ($ndb->red()) {
                    $router = $i->prop('ipaddr');
                } else {
                    $router = $i->prop('gateway') || $i->prop('ipaddr');
                }
            }
            if ($router) {
               $OUT .= "dhcp-option=tag:".$_->key.",option:router,$router\n";
            } else {
               $OUT .= "# dhcp-option=tag:".$_->key.",option:router,<UNDEFINED>";
            }

            my $dns_server = $_->prop('DhcpDNS') || '';
            if ($dns_server ne '') {
               $OUT .= "dhcp-option=tag:$interface,option:dns-server,$dns_server\n";
            }

            my $ntp_server = $_->prop('DhcpNTP') || '';
            if ($ntp_server ne '') {
               foreach (split(',',$ntp_server)) {
                   $OUT .= "dhcp-option=tag:$interface,option:ntp-server,$_\n";
               }
            }

            my $wins_server = $_->prop('DhcpWINS') || '';
            if ($wins_server ne '') {
               foreach (split(',',$wins_server)) {
                   $OUT .= "dhcp-option=tag:$interface,option:netbios-ns,$_\n";
               }
            }

            my $tftp_server = $_->prop('DhcpTFTP') || '';
            if ($tftp_server ne '') {
               foreach (split(',',$tftp_server)) {
                   $OUT .= "dhcp-option=tag:$interface,option:tftp-server,\"$_\"\n";
               }
            } else {
               if ( $tftpstatus eq 'enabled' && $role eq 'green' ) {
                   $OUT .= "dhcp-option=tag:$interface,option:tftp-server,\"$ipaddr\"\n";
               }
            }


        } else {
            $OUT .= "\n# DHCP disabled on ".$_->key."\n";
        } 
    }
    if ($tot_range_length > 0) {
        use esmith::HostsDB;
        my $hdb = esmith::HostsDB->open_ro() || die "Can't open hosts database: $!\n";
        foreach ($hdb->get_all_by_prop('type' => 'local')) {
            $_->prop('MacAddress') || next;
            $tot_range_length++;
        }
        $OUT .= "dhcp-lease-max=$tot_range_length\n";
    }
}

# Should be set when dnsmasq is definitely the only DHCP server on a
# network.
dhcp-authoritative

# Read dhcp reservations from dhcp-hostsfile. 
# See dhcp-hosts option for more informations.
dhcp-hostsfile=/etc/dnsmasq-dhcp-hosts



